/*REXX*/
/*****************************************************************/
/*LISTAPFT: REXX program to display APF table from CSTOR.        */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*             APFTAB - Undocumented in MVS DATA AREAS book.     */
/*                                                               */
/*                                                               */
/*                                                               */
/*****************************************************************/
numeric digits 30
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                          /* binary to hex       */

cvt = c2d(storage(10,4))                       /* point to cvt        */
ecvt = c2d(storage(d2x(cvt+140),4))            /* point to cvtecvt    */
@ecvt = STORAGE(D2X(X2D(@cvt)+140),4)          /* get CVTAUTHL addres */
@ecvt = D2X(C2D(@ecvt))                        /* binary to hex       */

@cvtauthl = STORAGE(D2X(X2D(@cvt)+484),4)      /* get CVTAUTHL addres */
@cvtauthl = D2X(C2D(@cvtauthl))                /* binary to hex       */
say '@cvtauthl= '@cvtauthl
say 'compare1= 'c2d('7ffff001'x)
Say 'The CVT  is located at '@cvt '('X2D(@cvt)')'
Say 'The eCVT is located at '@ecvt '('X2D(@ecvt)')'
Say 'The CVTAUTHL field points to '@cvtauthl '('X2D(@cvtauthl)')'
if X2D(@cvtauthl) <> c2d('7ffff001'x) then      /*dynamic list ?   @@ */
do          /* Static format  */
say 'Static Format'
apfcount  = C2D(STORAGE(D2X(X2D(@cvtauthl)+1),1)) /* APF libraries    */
apfllen   = C2D(STORAGE(D2X(X2D(@cvtauthl)+2),1)) /* length 1st entry */
@apfentry = X2D(@cvtauthl)+3          /* address of 1st APF entry     */
Say 'The APF table contains 'apfcount 'entries, as follows:'
Say ' '
Do i = 1 to apfcount                            /* loop thru storage  */
   apflib.i = STORAGE(D2X(@apfentry),apfllen)   /* get the APF entry  */
   @apfentry = @apfentry + apfllen + 1          /* bump to next entry */
   apfllen = C2D(STORAGE(D2X(@apfentry-1),1))   /* length of next  */
End
Say 'VOLSER --------------DATA SET NAME-----------------'
Say ' '
Do i = 1 to apfcount                            /* print the list     */
   Say SUBSTR(apflib.i,1,6) SUBSTR(apflib.i,7,44,' ') i
End
End         /* Static format  */
Else        /* Dynamic format */
  do
    say 'Dynamic Format'
    ecvtcsvt = c2d(storage(d2x(ecvt+228),4))   /* point to ecvtcsvt   */
    apfa = c2d(storage(d2x(ecvtcsvt+12),4))    /* addr start of list  */
    apht = c2d(storage(d2x(apfa+8),4))         /* addr 1st entry      */
    last = c2d(storage(d2x(apfa+12),4))        /* addr last entry     */
    apf. = ''
    inc = 0
    do forever
      apfdsn = storage(d2x(apht+24),44)        /* apf dataset name    */
      if substr(apfdsn,1,1) Â¬= '00'x then      /* 00 = deleted        */
        do
          apfesms = storage(d2x(apht+4),1)     /* sms flag            */
          if  bitand(apfesms,'80'x)  = '80'x then apfvol = '*SMS* '
          else apfvol = storage(d2x(apht+68),6)   /* apf volser       */
          line.inc =left(apfdsn,45)||apfvol
          inc = inc + 1
        end
      if apht = last then leave
      else apht = c2d(storage(d2x(apht+8),4))  /* addr next entry     */
    end
    apfcount = inc - 1
    Say 'The APF table contains 'apfcount 'entries, as follows:'

  do inc = 1 to apfcount                       /* for each line       */
    say ' 'line.inc                            /* build output line   */
  end
  end       /* Dynamic format */               /* set maximum lines   */
Exit
