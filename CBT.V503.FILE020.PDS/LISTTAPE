/*REXX*/
/*****************************************************************/
/*LISTTAPE: REXX program to display all online TAPE devices.     */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*                UCB - Device Unit Control Block                */
/*                                                               */
/* @ z/OS V2.2 the ULUT has been moved into 64 bit storage.      */
/*                                                               */
/*****************************************************************/
Address TSO ; "CLEAR" ; Numeric Digits 20    /* 20 can handle 64 bits */
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                    /* binary to dec to hex      */
@cvtucba  = STORAGE(D2X(X2D(@cvt)+1252),4)   /* get CVTUCBA address   */
@cvtucba  = D2X(C2D(@cvtucba))                 /* binary to dec to hex*/
@cvtixav  = STORAGE(D2X(X2D(@cvt)+124),4)      /* get CVTIXAVL addres */
@cvtixav  = D2X(C2D(@cvtixav))                       /* binary to hex */
@iovtadr  = STORAGE(D2X(X2D(@cvtixav)+208),4)  /* get IOVT address    */
@iovtadr  = D2X(C2D(@iovtadr))                       /* binary to hex */
@ulutadr  = STORAGE(D2X(X2D(@iovtadr)+8),4)    /* get IOVT address    */
@ulutadr  = D2X(C2D(@ulutadr))                       /* binary to hex */
if @ulutadr = 0 then                            /* zOS V2.2 or higher */
  do                                            /* or V2.1/V1.13 w/PTF*/
   Say 'zOS:'MVSVAR(SYSOPSYS)' Ulut is in 64 bit storage'
   @ulutadr  = STORAGE(D2X(X2D(@iovtadr)+440),8)  /* get IOVT address */
   @ulutadr  = D2X(C2D(@ulutadr))                    /* binary to hex */
  end
ulu      = STORAGE(@ulutadr,64)             /* get 64 byte ulu header */
uluname  = SUBSTR(ulu,1,4)                         /* ulut eyecatcher */
ulutype  = SUBSTR(ulu,5,1)
If ulutype = '01'x Then off = 0                    /* ULUT Type 1     */
If ulutype = '02'x Then off = 8                    /* ULUT Type 2     */
If ulutype = '03'x Then                            /* ULUT Type 3     */
 Do
   ulufirst = SUBSTR(ulu,17,8)          /* addr of 1st ULU element(64)*/
   uluucbno = SUBSTR(ulu,29,4)          /* # of ucb's this I/O config */
   off = 12
 End
Else
 Do                                     /* Ulut type 1 or 2           */
   ulufirst = SUBSTR(ulu,13+off,4)      /* address of 1st ULU element */
   uluucbno = SUBSTR(ulu,17+off,4)      /* # of ucb's this I/O config */
 End
 uluttape = SUBSTR(ulu,21+off,4)        /* # of TAPE UCBs in I/O cfg  */
 ulutcomm = SUBSTR(ulu,25+off,4)        /* # of COMM UCBs in I/O cfg  */
 ulutdasd = SUBSTR(ulu,29+off,4)        /* # of DASD UCBs in I/O cfg  */
 @uluchn  = C2X(ulufirst)
Say 'CVT: '@cvt' CVTUCBA: '@cvtucba' ===> TAPE UCB scan started: 'TIME()
Say '            CVTIXAV:'@cvtixav' IOVT:'@iovtadr' ULUT: '@ulutadr
Say '                                        ULUT Type: 'C2D(ulutype)
Say ' '
Say '     This I/O configuration has 'C2D(uluttape)' TAPE UCBs defined!'
Say ' '
@ucbchn = @cvtucba
i = 0 ; j = 0
Do i = 1 to C2D(uluucbno)               /* scan the ULUT for ALL DASD */
    uluelmnt = STORAGE(@uluchn,12)       /* get 12 byte ULUT element */
    uluucbad = SUBSTR(uluelmnt,9,4)     /* 1st ucb this device class */
    uluucbad = C2X(uluucbad) ; @ucbchn = uluucbad
   ucb     = STORAGE(@ucbchn,24)    /* get 24 byte ucb common segment */
   ucbid   = SUBSTR(ucb,3,1)                 /* get the 'FF'x id byte */
   ucbstat = SUBSTR(ucb,4,1)              /* status byte '80'x online */
   ucbchan = SUBSTR(ucb,5,2)                /* channel address in hex */
   ucbnxucb= SUBSTR(ucb,9,4)                   /* pointer to next ucb */
   ucbtbyt3= SUBSTR(ucb,19,1)                 /* device '80'x is TAPE */
   ucbtbyt4= SUBSTR(ucb,20,1)           /* contains device type codes */
   If (BITAND(ucbstat,'80'x)='80'x & BITAND(ucbtbyt3,'80'x)='80'x) Then Do
      j = j + 1; device = 'Unknown'
      ioaddr = C2X(ucbchan)
      If BITAND(ucbtbyt4,'80'x)='80'x Then device = '3480   '
      If BITAND(ucbtbyt4,'01'x)='01'x Then device = '2400   '
      If BITAND(ucbtbyt4,'03'x)='03'x Then device = '3420   '
      If BITAND(ucbtbyt4,'81'x)='81'x Then device = '3490E  '
      @ucbchn=D2X(X2D(@ucbchn)+24)            /* bump to TAPE segment */
      ucbtape = STORAGE(@ucbchn,24)       /* get 24 byte TAPE segment */
      ucbfser = SUBSTR(ucbtape,5,6)              /* volser if mounted */
      If C2X(ucbfser) = '000000000000' Then ucbfser = '-idle-'
      Say RIGHT(j,3,' ') RIGHT(ioaddr,3) STRIP(device) ucbfser
   End
   @uluchn  = D2X(x2d(@uluchn)+12)
End
Say ' '
Say 'Last UCB after 'i' blocks scanned.  Completed: 'TIME()
Exit
